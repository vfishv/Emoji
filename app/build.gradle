/*
 * Copyright (C) 2016 - Niklas Baudy, Ruben Gees, Mario Đanić and contributors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
  compileSdkVersion rootProject.ext.compileSdkVersion as int

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  defaultConfig {
    applicationId "com.vanniktech.emoji.sample"
    minSdkVersion rootProject.ext.minSdkVersion as int
    targetSdkVersion rootProject.ext.targetSdkVersion as int
    versionCode rootProject.ext.versionCode as int
    versionName rootProject.ext.versionName as String

    vectorDrawables.useSupportLibrary = true

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  archivesBaseName = "app-$versionName"

  def shouldSign = project.hasProperty('RELEASE_STORE_FILE') && project.hasProperty('RELEASE_STORE_PASSWORD') && project.hasProperty('RELEASE_KEY_ALIAS') && project.hasProperty('RELEASE_KEY_PASSWORD')

  if (shouldSign) {
    signingConfigs {
      release {
        // http://stackoverflow.com/a/21020469/1979703
        storeFile file(RELEASE_STORE_FILE)
        storePassword RELEASE_STORE_PASSWORD
        keyAlias RELEASE_KEY_ALIAS
        keyPassword RELEASE_KEY_PASSWORD
      }
    }
  }

  buildTypes {
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
      shrinkResources true
      zipAlignEnabled true

      if (shouldSign) {
        signingConfig signingConfigs.release
      }
    }
  }
}

dependencies {
  implementation project(':emoji-ios')
  implementation project(':emoji-google')
  implementation project(':emoji-google-compat')
  implementation project(':emoji-twitter')
  implementation project(':emoji-kotlin')

  implementation rootProject.ext.supportAppCompat
  implementation rootProject.ext.supportRecyclerView

  debugImplementation rootProject.ext.leakCanaryDebug

  androidTestImplementation deps.support.test.espresso.core
  androidTestImplementation deps.support.test.runner
  androidTestImplementation deps.support.test.rules
  androidTestImplementation deps.fastLaneScreenGrab
}

configurations.all {
  resolutionStrategy {
    force rootProject.ext.supportAnnotations
  }
}
